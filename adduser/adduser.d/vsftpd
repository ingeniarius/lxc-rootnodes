#!/usr/bin/perl -l
#
# lxc-add vsftpd script
# Rootnode, http://rootnode.net
#
# Copyright (C) 2012 Marcin Hlybin
# All rights reserved.
#
# The script creates vsftpd configuration

use warnings;
use strict;
use Readonly;
use Tie::File;
use Net::SSH qw(ssh);
use DBI;

# Configuration
Readonly my $LXC_DIR => '/lxc/user';

# Check paths
-d $LXC_DIR or die "\$LXC_DIR ($LXC_DIR) not found.\n";

# Get arguments
die "Uneven number of arguments." if @ARGV % 2;
my %arg = @ARGV;

# Mandatory arguments
my $uid         = $arg{uid}         or die "Uid not specified.";
my $user_name   = $arg{user_name}   or die "User name not specified.";
my $server_no   = $arg{server_no}   or die "Server number not specified.";
my $server_type = $arg{server_type} or die "Server type not specified."; 
my $vsftpd_host = $arg{vsftpd_host} or die "Vsftpd host host not specified.";

# Generate password
my $password = `apg -a 0 -n 1 -m 12 -x 16 -M NCL` or die "Cannot generate password";
chomp $password;

# Alter vsftpd pam file
my $pam_file = "$LXC_DIR/$user_name/rootfs/home/etc/pam.d/vsftpd";
tie my @vsftpd_file, 'Tie::File', $pam_file;
for (@vsftpd_file) {
	s/user=.+?-ftp\s/user=$uid-ftp /;
	s/passwd=.+?\s/passwd=$password /;
	s/host=.+?\s/host=$vsftpd_host /;
}
untie @vsftpd_file;

# Create MySQL user
ssh($vsftpd_host, qq{ mysql -Nse "GRANT SELECT ON ftp.users TO '$uid-ftp' IDENTIFIED BY '$password'" });
ssh($vsftpd_host, qq{ mysql -Nse "FLUSH PRIVILEGES" });
